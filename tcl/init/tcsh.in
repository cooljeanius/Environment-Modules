if ($?tcsh) then
  set modules_shell="tcsh"
else
  set modules_shell="csh"
endif

setenv MODULESHOME $MODULESHOME

if ( -f /soft/tcl8.4.5/bin/tclsh8.4 ) then
   set TCLSH="/soft/tcl8.4.5/bin/tclsh8.4"
else 
   if ( -f /usr/bin/tclsh ) then
      set TCLSH="/usr/bin/tclsh"
   else 
      if ( -f /bin/tclsh ) then
         set TCLSH="/bin/tclsh"
      else 
         set TCLSH=""
      endif
   endif
endif

if ( $?histchars ) then
  set _histchars = $histchars
  if ($?prompt) then
    alias module 'unset histchars;set _prompt="$prompt";eval `'$TCLSH $MODULESHOME'/modulecmd.tcl $modules_shell \!*`;set histchars = $_histchars; set prompt="$_prompt";unset _prompt'
  else
  alias module 'unset histchars;eval `'$TCLSH $MODULESHOME'/modulecmd.tcl $modules_shell \!*`;set histchars = $_histchars'
  endif
else
  if ($?prompt) then
    alias module 'set _prompt="$prompt";set prompt="";eval `'$TCLSH $MODULESHOME'/modulecmd.tcl $modules_shell \!*`;set prompt="$_prompt";unset _prompt'
else
  alias module 'eval `'$TCLSH $MODULESHOME'/modulecmd.tcl $modules_shell \!*`'
  endif
endif

source $MODULESHOME/init/modulerc

